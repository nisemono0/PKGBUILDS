# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

# Added some patches

pkgname=dmenu
pkgver=5.2
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'glibc' 'coreutils' 'libx11' 'libxinerama' 'libxft' 'freetype2' 'fontconfig' 'libfontconfig.so')
source=(https://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz
        "dmenu-border-5.2.diff"
        "dmenu-center-5.2.diff"
        "dmenu-dynamic-buffer-5.2.diff"
        "dmenu-linesbelowprompt-and-fullwidth-5.2.diff"
        "dmenu-locale-mod-5.2.diff"
        "dmenu-mousesupport-5.2.diff"
        "dmenu-mousesupport-motion-5.2.diff"
        "dmenu-numbers-5.2.diff"
        "dmenu-preselect-5.2.diff"
)

b2sums=('f827f0d0d935f9da8103dbb73ed8b67818f1213d2f8e5a406de5e8c9496ea0479b4ccb50d3a29004abc1e4e679cac177163c02458ed144d323bb50fc120f936d'
        'a774c4c85f88f001808459cf9a26ffb7c87e3ce7324893aae32735b24891f6b08bb28e5b54d1af99c39a1a85a1e2a98172b6814272ac6da228160996254719fd'
        'ca14a5011a0da2e6d6e12d1bc261ce5708f1a39dfff90a34aa586d9a310be8a698ab0d6b33be041b66a40b47f4dc12b7242bd52eb34e5de03f60cf3d0d06ac01'
        'ee0ddf13cfad5a8cad3da3c560090ac80ba45b9538ec73d1d80a1ee692f81130a22d7de4ca80f9d2f25b78e7a88ce90d8d191550775767eb4b3aa9990f75eded'
        '063d59109f3d66e783e1b1998aa65886501131119aea42bfdb580ad32447cf152b0dc0f00e1aa21aabd9daa4fabaaf52972cb61c14e583e940299ce12fb98827'
        '5b9b29d5fa2519d9f19108f5412d0fa45c480ccc6f2c4ba252b103703e7547dc0039358035ac1eff5da129b0103ba63fae9b20dbe9b579ab7232b5b82f0f1cbe'
        'ae663382cae3562c1c6de738588b10e8d90a7e9b02cc8bcb18e31f9668629f431557e050f512ea0feab2add39ef20ccab8a40c45e2371efd026ce9366b011518'
        'aec5f5c10a122201dae8d3ab1cd856b362fa18fb575aeb27afa7056ee94b9ebcbbc09f12607583873360b9e6fdac7a01378b52bb006f9d983dd0a98be01387e5'
        '3292ae62790a6110c33d13eae0c00b93df2bb7a13b40b9732a0369f7de2b1010d838eaf6707e50bd87e2c407e81518d5a586d4305b4c58d249d4da2c27c3f338'
        '079f153ea14c0d73f2f5c1ae2a7503a42cee5c89c70195f8fb238113b35ff79b47a769cba58e79dd6885dca947bdefdb4ab256c78fdbdd7461eb38693ed1be44'
)

prepare() {
  cd ${pkgname}-${pkgver}
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
  
  patch -p1 -i "$srcdir/dmenu-center-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-border-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-mousesupport-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-mousesupport-motion-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-numbers-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-linesbelowprompt-and-fullwidth-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-locale-mod-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-dynamic-buffer-5.2.diff"
  patch -p1 -i "$srcdir/dmenu-preselect-5.2.diff"
}

build() {
  cd ${pkgname}-${pkgver}
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
