# Edited

_pkgname=dmenu
pkgname="$_pkgname-nise-git"
pkgver=r621.d893c63
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'glibc' 'coreutils' 'libx11' 'libxinerama' 'libxft' 'freetype2' 'fontconfig' 'libfontconfig.so')
makedepends=('git')
provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")
source=("git+https://git.suckless.org/$_pkgname"
        "dmenu-border-git.diff"
        "dmenu-center-git.diff"
        "dmenu-dynamic-buffer-git.diff"
        "dmenu-linesbelowprompt-and-fullwidth-git.diff"
        "dmenu-locale-mod-git.diff"
        "dmenu-mousesupport-git.diff"
        "dmenu-mousesupport-motion-git.diff"
        "dmenu-movepointer-git.diff"
        "dmenu-numbers-git.diff"
        "dmenu-preselect-git.diff"
)

b2sums=('SKIP'
        'ac327cbf65c2e86ec7987658090a95860cfc3b1765b6bf9c6be35ac0c34fbd11ac32cfa6ce1cda17886a95bb13b0b0f188811274678fd46b2f808d1a81fa1fce'
        '4c836ab723292fdd6e9a87356a08794c110598454a3f2b61708f5b894039ed6a5ec65b95471fa9154d918b04c88a92cae921566a9581e453dab10570f6f25352'
        '2c8af3c77c6dad6477906ba6d57a2f4ccf30e14e95895b1640611dcb29accf71900157a6d841bafe851e69ac18ce99d362286e14a3d7485187c550b543fd8e37'
        'f6edfa67421aea15ba37d139fba2dfef0a7b2c9b99f681e2e7f85566c2903e319c3435e24053820cb2cbc52dac069e1a998511263109de62b2506d469c950365'
        'af67ef88488e35be7c5f2bedc55bb6472ba824f3aea853c98eadecd51bbe0e592ab1d8c3ec9aa12af106c72cf699638cf8cdf30e6c1cc4fd5fc62a7fd6b1958c'
        'cd5c3e20b4bb55f36b911c89ec70ca8228d38b5d1094fbc56413a21982188a5d80c16ace70328a13173c69d6ac1f0caf60e1f049877d77e426f025d9e1c668b6'
        'f1e6a490368af620e033a91386e4371ba522af5706309b53975f2eb7be409d286508b714581d62bf717a91b30d9436cb9bcdef5aa26b2bdaf8cf520a045e5b42'
        '88a3821a749622aaa1ce3a7c5b18e56f91344e439dfd3ea7ff1e3e93d91e6cd5f636b4a3b659f79aefcb236f42b5d39fa51e68e6585d622e9846fa0757cf19bf'
        'b98de06aa9467491161f0cf5fcf3a64fa262b96c5ed67eae6bfe4f5117ea274ce10abeabfa066007d68a18a67a9e3d81ec180588770b99b40a92f17a73ce88d1'
        'df50bc9963c36d3b1a8ef39a7b28c65fc99fd241e08672aa91503bf8b59c7cd88c52ebd31cac4f4ddd1496c427b61b5b2be1e68ded74cd0171ec65bbfc56e6a5'
)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


prepare() {
  cd ${_pkgname}
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk

  patch -p1 -i "$srcdir/dmenu-center-git.diff"
  patch -p1 -i "$srcdir/dmenu-border-git.diff"
  patch -p1 -i "$srcdir/dmenu-mousesupport-git.diff"
  patch -p1 -i "$srcdir/dmenu-mousesupport-motion-git.diff"
  patch -p1 -i "$srcdir/dmenu-numbers-git.diff"
  patch -p1 -i "$srcdir/dmenu-linesbelowprompt-and-fullwidth-git.diff"
  patch -p1 -i "$srcdir/dmenu-locale-mod-git.diff"
  patch -p1 -i "$srcdir/dmenu-dynamic-buffer-git.diff"
  patch -p1 -i "$srcdir/dmenu-preselect-git.diff"
  patch -p1 -i "$srcdir/dmenu-movepointer-git.diff"
}

build() {
  cd ${_pkgname}
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11 \
    FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${_pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
