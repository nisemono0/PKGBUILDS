_pkgname=Overmix
pkgname=overmix-git
pkgver=r860.f5fa335
pkgrel=1
pkgdesc='Automatic anime screenshot stitching in high quality'
arch=('x86_64')
url='https://github.com/spillerrec/Overmix'
license=('GPL3')
depends=(
  'qt5-base'
  'qt5-x11extras'
  'ffmpeg'
  'zlib'
  'xz'
  'libpng'
  'libjpeg'
  'libraw'
  'fftw'
  'pugixml'
  'lcms2'
  'eigen'
  'qcustomplot'
  'libxcb'
)
conflicts=("overmix")
provides=("overmix")
makedepends=('cmake' 'boost' 'png++')

source=("git+${url}"
        "overmix.desktop")

sha256sums=('SKIP'
            'e9e71bf5523fb4acfd7e01d0db90f42626ed9cd7fb20f57efa8fe40a69639627')

pkgver() {
	cd $_pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    exit
}

prepare() {
    sed -i -E 's/AVCodec \*codec/const AVCodec \*codec/' "${_pkgname}/src/video/VideoStream.cpp"
    cmake \
        -B build \
        -S "${_pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_FLAGS="$CFLAGS" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS" \
        -DCMAKE_INSTALL_PREFIX=/usr
}

build() {
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -Dm644 "${_pkgname}/license.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 "overmix.desktop" -t "${pkgdir}/usr/share/applications"
}
