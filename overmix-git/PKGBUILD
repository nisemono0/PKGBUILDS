_pkgname=Overmix
pkgname=overmix-git
pkgver=r896.ebc2d31
pkgrel=6
pkgdesc='Automatic anime screenshot stitching in high quality'
arch=('x86_64')
_commit="ebc2d3128a4024a7cb267ad0ecec210aa772efc7"
url='https://github.com/spillerrec/Overmix'
license=('GPL3')
depends=(
    'qt5-base'
    'qt5-x11extras'
    'ffmpeg'
    'zlib'
    'xz'
    'libpng'
    'libjpeg'
    'libraw'
    'fftw'
    'pugixml'
    'lcms2'
    'eigen'
    'qcustomplot'
    'libxcb'
    'libwgpu-native'
)
conflicts=("overmix")
provides=("overmix")
makedepends=('cmake' 'boost' 'png++' 'git')

source=("git+${url}#commit=${_commit}"
        "overmix.desktop"
        "newer-ffmpeg-version-fix.patch"
        "load-max-frames.patch")

sha256sums=('SKIP'
            'e9e71bf5523fb4acfd7e01d0db90f42626ed9cd7fb20f57efa8fe40a69639627'
            '3f88cb6b86ee7e61c2b767fca0084d4d29a9595bcda3bd9e9b98b63b015f7c3c'
            'ae0d9cadee8dac53fac720f9713d644fc6d22bf0914f885e54fb1262e1190107')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    patch --strip=1 -u --input="${srcdir}/newer-ffmpeg-version-fix.patch" "${srcdir}/${_pkgname}/src/video/VideoFrame.cpp"
    patch --strip=1 -u --input="${srcdir}/load-max-frames.patch" "${srcdir}/${_pkgname}/interface/gui/importers/VideoImporter.cpp"
    cmake \
        -B build \
        -S "${_pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_FLAGS="$CFLAGS" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWEBGPU_LIBRARY=/usr/lib/libwgpu_native.so \
        -DWEBGPU_INCLUDE_DIR=/usr/include/webgpu
}

build() {
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -Dm644 "${_pkgname}/license.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 "overmix.desktop" -t "${pkgdir}/usr/share/applications"
}
