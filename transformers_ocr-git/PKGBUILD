# Maintainer: Ren Tatsumoto <tatsu at autistici dot org>
# Original https://aur.archlinux.org/packages/transformers_ocr

# Patched to work with scrot and uses the latest git version

_pkgname=transformers_ocr
pkgname=transformers_ocr-git
pkgver=r97.16a441b
pkgrel=1
pkgdesc="An OCR tool for manga using maim with Transformers. (Patched to work with scrot)"
arch=(any)
url="https://github.com/Ajatt-Tools/transformers_ocr"
license=("GPL3")

source=(
    "git+${url}"
    "SCROT_PATCH.diff"
)

depends=('python' 'python-pip' 'libnotify')
optdepends=(
     'maim: taking screenshots on Xorg'
     'scrot: taking screenshots on Xorg (alternative to maim)'
     'xclip: copying to the clipboard on Xorg'

     'grim: taking screenshots on Wayland'
     'slurp: selecting a region on Wayland'
     'wl-clipboard: copying to the clipboard on Wayland'

     'gnome-screenshot: taking screenshots on GNOME'
     'spectacle: taking screenshots on KDE'
)

conflicts=("transformers_ocr")
provides=("transformers_ocr")

sha256sums=(
    'SKIP'
    'da80e10be02ea9b0f15cbf7ce113ae6c7a6e51da9059476c0b4c4d69c5eb707a'
)

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    exit
}

prepare() {
    cd -- "${_pkgname}"
    patch --strip=1 -u --input="${srcdir}/SCROT_PATCH.diff" "src/transformers_ocr.py"
}

package() {
    cd -- "${_pkgname}"
    make PREFIX="${pkgdir}/usr" install
}
