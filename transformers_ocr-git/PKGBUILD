# Maintainer: Ren Tatsumoto <tatsu at autistici dot org>
# Original https://aur.archlinux.org/packages/transformers_ocr

# Patched to work with scrot and uses the latest git version

_pkgname=transformers_ocr
pkgname=transformers_ocr-git
pkgver=r102.c5ead66
pkgrel=1
pkgdesc="An OCR tool for manga using maim with Transformers. (Patched to work with scrot)"
arch=(any)
url="https://github.com/Ajatt-Tools/transformers_ocr"
license=("GPL3")

source=(
    "git+${url}"
    "scrot-freeze.diff"
)

depends=('python' 'python-pip' 'libnotify')
optdepends=(
     'maim: taking screenshots on Xorg'
     'scrot: taking screenshots on Xorg (alternative to maim)'
     'xclip: copying to the clipboard on Xorg'

     'grim: taking screenshots on Wayland'
     'slurp: selecting a region on Wayland'
     'wl-clipboard: copying to the clipboard on Wayland'

     'gnome-screenshot: taking screenshots on GNOME'
     'spectacle: taking screenshots on KDE'
)

conflicts=("transformers_ocr")
provides=("transformers_ocr")

sha256sums=(
    'SKIP'
    '6d55ab26c88ed82b2e5edd155b923396219504415af2ed4500a6e4dc92d02941'
)

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    exit
}

prepare() {
    cd -- "${_pkgname}"
    patch --strip=1 -u --input="${srcdir}/scrot-freeze.diff" "src/transformers_ocr.py"
}

package() {
    cd -- "${_pkgname}"
    make PREFIX="${pkgdir}/usr" install
}
