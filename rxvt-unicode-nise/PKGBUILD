# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

# Edited to include the half screen bug patch

pkgbase=rxvt-unicode-nise
_pkgname=('rxvt-unicode' 'rxvt-unicode-terminfo')
pkgname=('rxvt-unicode-nise' 'rxvt-unicode-terminfo-nise')
pkgver=9.31
pkgrel=8
arch=('x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL')
makedepends=(
  'libxft'
  'libxt'
  'libxext'
  'libxmu'
  'perl'
  'startup-notification'
  'libnsl'
  'libptytty'
  'signify'
)
source=(
  "$_pkgname-$pkgver.tar.bz2::http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2"
  "$_pkgname-$pkgver.tar.bz2.signature::http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2.sig"
  "$_pkgname-$pkgver.pub::http://dist.schmorp.de/signing-key.pub"
  'urxvt.desktop'
  'urxvtc.desktop'
  'urxvt-tabbed.desktop'
  '7-bit-queries.patch'
  'perl-5.38.patch'
  'lines-rewrap.patch'
  '24-bit-color.patch'
  'enable-wide-glyphs.patch'
  'improve-font-rendering.patch'
  'sixel-support.patch'
)

b2sums=(
  '439a8c33b7260e0f2fd68b8a4409773c06c3bb7623b5dfbbb0742cc198c9fd25e8a247907878639db5fac3dcd3b6be3d839347787bcf08ca602ae246607f750b'
  '1c58b1d53c72dbfae4f9302f5903365f84e85b2a1e9846d9dd9c712a7900e73a94621a41bee6dd75d690df8ae95d1d987d93ca4355eb8d8c423c57680df0edcf'
  '6d6bd90b77e67ccb876f0c78c710c9e1b82767a19aeadaac9310e5628b791586fc8475ad5179eaa2fee386ae80aae916226167ec92c5af309bba4052238326c8'
  '79a5f759dc6568c9193d3c533ba6c649aafef7eb545abf5a87cdf8d877ce2c25ae4a841e23967cc63f8048eb140343f0fe1a8327642410190c5da60415c50d3c'
  '420a8820c165a2137cf9936acdc597ec696a5d75c30f8bbcfb0f3b75d04fdf4a9a4e3ae38a35372cfbf0497b2d115902aa18b2b38d292cf881fc19d1c113b7df'
  'abb9325d47d42f813e3054430c7451d5b8141a2285aa8082feb0ee24f8c0b3bc3011854d0d964da5e9e4296770f1ce8ba5843fe8b0bcf2cbcec4f6ea8e6a720a'
  '94b110f9901786c398c313199da17b5981644241de2385208d36a3cccbf029c518ee619bbadff0348405bc01cd8f24abf41aa361b2f140178345197c2b476e03'
  'dcaeac1c3822bf27d0ea4ac3ae6a8852c88420b3b30a5b31ec9022afbf4f65aa29b1e11e0b5e3f6098de057983d886b6a54b5e3ce489ac93320245a6d6f64703'
  '06b8a321a59c92ad781f882e454a4868a5b9e6bf482217568a6ed8a2e82e5e2e604bcea1efd79aa6dbaffb6c1e12416f4232966e718b2001142082a57e15468f'
  'ba382f5b1b2d7494a2faee2674530fae21aa36ce9e6aea7375c354fa8036ef37f34759b164658856cb4bebbc8f34be79dc26eca83670b945e956dae1d54bf842'
  '83cad5f2583ea152c7da1fc804a9ce7e7ee8d7b9735645b647172a8d0512c8cdcf44b45e16ccd3b8770c971749aecfc8d172009bffb479ca00d12b7697b012cd'
  '05faae03a36b71fa5a9105f143b8e2d084014857cc4012c25a531808392c5a6e48ea72cd0857beb494d76b7b0a7943ec8da3e28e411d800e29b8ac6ae4e21788'
  '779722ba0b7215464cc35081fc9592910499646602fdb2b772d6329545e8ec0099fb4e0a3c4a63bbc4d900838c28fee5a4865c48cfc5192d1538d0676d03bc4e'
)

prepare() {
  # hacking around to validate with signify
  mv -v "$_pkgname-$pkgver.tar.bz2."{signature,sig}
  signify -V -p "$_pkgname-$pkgver.pub" -m "$_pkgname-$pkgver.tar.bz2"

  patch -d "${_pkgname}-${pkgver}" -p0 < 7-bit-queries.patch
  patch -d "${_pkgname}-${pkgver}" -p0 < perl-5.38.patch # Locale fix for perl 5.38
  patch -d "${_pkgname}-${pkgver}" -p0 < lines-rewrap.patch
  patch -d "${_pkgname}-${pkgver}" -p0 < 24-bit-color.patch
  patch -d "${_pkgname}-${pkgver}" -p0 < enable-wide-glyphs.patch
  patch -d "${_pkgname}-${pkgver}" -p0 < improve-font-rendering.patch
  #patch -d "${_pkgname}-${pkgver}" -p0 < sixel-support.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  # we disable smart-resize (FS#34807)
  # do not specify --with-terminfo (FS#46424)
  # do not specify --disable-frills (FS#77474)
  # workaround ncurses --disable-root-access (FS#79143)
  export TIC="/usr/bin/tic -o${srcdir}/terminfo"
  ./configure \
    --prefix=/usr \
    --enable-256-color \
    --enable-combining \
    --enable-fading \
    --enable-font-styles \
    --enable-iso14755 \
    --enable-keepscrolling \
    --enable-lastlog \
    --enable-mousewheel \
    --enable-next-scroll \
    --enable-perl \
    --enable-pointer-blank \
    --enable-rxvt-scroll \
    --enable-selectionscrolling \
    --enable-slipwheeling \
    --disable-smart-resize \
    --enable-startup-notification \
    --enable-transparency \
    --enable-unicode3 \
    --enable-utmp \
    --enable-wtmp \
    --enable-xft \
    --enable-xim \
    --enable-xterm-scroll \
    --enable-wide-glyphs \
    --enable-24-bit-color \
    --disable-pixbuf
  make
}

package_rxvt-unicode-nise() {
  pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt) with some added patches'
  depends=(
    'rxvt-unicode-terminfo-nise'
    'libxft'
    'perl'
    'startup-notification'
    'libnsl'
    'libptytty'
    'libxext'
    'libxmu'
  )
  optdepends=('gtk2-perl: to use urxvt-tabbed')
  provides=('rxvt-unicode')
  conflicts=('rxvt-unicode')

  # install freedesktop menu
  for _f in urxvt urxvtc urxvt-tabbed; do
    install -Dm 644 ${_f}.desktop "${pkgdir}/usr/share/applications/${_f}.desktop"
  done

  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed"
}

package_rxvt-unicode-terminfo-nise() {
  pkgdesc='Terminfo files for urxvt'
  provides=('rxvt-unicode-terminfo')
  conflict=('rxvt-unicode<=9.18-6' 'rxvt-unicode-terminfo')
  install -dm 755 "${pkgdir}/usr/share/"
  mv terminfo "${pkgdir}/usr/share/"
}

# vim: ts=2 sw=2 et:
