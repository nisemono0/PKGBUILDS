_pkgname=wgpu-native
pkgname=libwgpu-native
pkgver=0.19.4.1
pkgrel=1
pkgdesc='Native WebGPU implementation in Rust, based on wgpu-core'
url='https://github.com/gfx-rs/wgpu-native'
arch=('x86_64')
license=('MIT')
makedepends=('rust' 'llvm' 'clang')
provides=('libwgpu_native.so')
_commit="d89e5a93f5fd0bb3ce28f09571dbbefea37712c9" # tags/v0.19.4.1
source=("git+https://github.com/gfx-rs/wgpu-native#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --tags | awk -F '-' '{print $1}' | sed 's/v//'
}

prepare() {
    cd "$_pkgname"
    git submodule update --init
}

build() {
    cd "$_pkgname"
    make lib-native-release
}

package() {
    #$srcdir/$_pkgname/target/release/libwgpu_native.so
    #$srcdir/$_pkgname/ffi/webgpu-headers/webgpu.h
    #$srcdir/$_pkgname/ffi/wgpu.h

    # lib
    install -Dm 755 -t "$pkgdir/usr/lib/" "$srcdir/$_pkgname/target/release/libwgpu_native.so"
    # include
    install -dm 775 "$pkgdir/usr/include/$_pkgname"
    install -Dm 644 -t "$pkgdir/usr/include/webgpu" "$srcdir/$_pkgname/ffi/wgpu.h"
    install -Dm 644 -t "$pkgdir/usr/include/webgpu" "$srcdir/$_pkgname/ffi/webgpu-headers/webgpu.h"
}
