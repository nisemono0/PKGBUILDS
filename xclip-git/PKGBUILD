_pkgname=xclip
pkgname=$_pkgname-git
pkgver=r173.b372f73
pkgrel=1
pkgdesc='Command line interface to the X11 clipboard'
url='https://github.com/astrand/xclip'
arch=('x86_64')
license=('GPL')
depends=('libxmu')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	./bootstrap
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
}
