# Maintainer: Ren Tatsumoto <tatsu at autistici dot org>
# Original https://aur.archlinux.org/packages/transformers_ocr

# Patched to work with scrot

_pkgname=transformers_ocr
pkgname=transformers_ocr-nise
pkgver=0.19
pkgrel=1
pkgdesc="An OCR tool for manga using maim with Transformers. (Patched to work with scrot)"
arch=(any)
url="https://github.com/Ajatt-Tools/transformers_ocr"
license=("GPL3")
source=(
    "${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
    "scrot-freeze.diff"
)
depends=('python' 'python-pip' 'libnotify')
optdepends=(
    'maim: taking screenshots on Xorg'
    'scrot: taking screenshots on Xorg (alternative to maim)'
    'xclip: copying to the clipboard on Xorg'

    'grim: taking screenshots on Wayland'
    'slurp: selecting a region on Wayland'
    'wl-clipboard: copying to the clipboard on Wayland'

    'gnome-screenshot: taking screenshots on GNOME'
    'spectacle: taking screenshots on KDE'
    'xfce4-screenshooter: taking screenshots on XFCE'
)

conflicts=("transformers_ocr" "transformers_ocr-nise-git")
provides=("transformer_ocr")

sha256sums=(
    '7fe482424717f759573218e6b3228090a6dec665e93b070071368242150df6b3'
    'e0bef6d3940cfbea7d0ee7f04255b91c3d61f84e5e1db9002e46dc5123ec9563'
)

prepare() {
    cd -- "${_pkgname}-${pkgver}"
    patch --strip=1 -u --input="${srcdir}/scrot-freeze.diff" "src/transformers_ocr.py"
}

package() {
    cd -- "${_pkgname}-${pkgver}"
    make PREFIX="${pkgdir}/usr" install
}
