# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Roman Zimbelmann <romanz@lavabit.com>

_pkgname=ranger
pkgname=ranger-nise
pkgver=1.9.4
pkgrel=1
pkgdesc='Simple, vim-like file manager'
arch=('any')
url='https://ranger.github.io'
license=('GPL')
depends=('python')
optdepends=(
    'atool: for previews of archives'
    'elinks: for previews of html pages'
    'ffmpegthumbnailer: for video previews'
    'highlight: for syntax highlighting of code'
    'imagemagick: auto-rotate image previews based on EXIF data'
    'libcaca: for ASCII-art image previews'
    'lynx: for previews of html pages'
    'mediainfo: for viewing information about media files'
    'odt2txt: for OpenDocument texts'
    'perl-image-exiftool: for viewing information about media files'
    'poppler: for pdf previews'
    'python-chardet: in case of encoding detection problems'
    'python-pillow: for in-terminal preview of images in Kitty'
    'sudo: to use the "run as root"-feature'
    'transmission-cli: for viewing bittorrent information'
    'ueberzug: for previews of images'
    'w3m: for previews of images and html pages')
checkdepends=('python-pytest')
makedepends=(python-{build,installer,wheel}
             python-setuptools)
source=(
    "https://ranger.github.io/${_pkgname}-${pkgver}.tar.gz"
    'libcurses-fix.patch'
)
sha512sums=(
    '18cd1d66838a5f85176e6b7502521a7e449aaa0b12e97d62142d39f980c529c5b97a50f6fb8a65adb64612fa80450b1e13047327f7bcc005a494af4ab321e4d4'
    '30bcc8e7c3d348bde6459a59c77ef338cfecbf3cc6f636bd8f5c5ca8b68ba0c2c5b28ef7915124df5c6c340a5b40fb61415b66bd5a779d8d1f7b609c8f4cb412'
)

prepare() {
  cd "${_pkgname}-${pkgver}"
  # fixes libcurses endwin problem
  patch -p0 < ../libcurses-fix.patch
  # fixes https://github.com/ranger/ranger/issues/1853
  # upsteam fix was never released:
  # https://github.com/ranger/ranger/commit/2241cac899536a94c032a58bb7f1bf921980687e
  sed -i 's/xdg-open -- /xdg-open /' "ranger/config/rifle.conf"
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build -wn
}

check() {
  cd ${_pkgname}-${pkgver}
  local _pyver=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  export PYTHONPATH="$PWD/lib.linux-$CARCH-cpython-$_pyver"
  export TERM=linux
  pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
